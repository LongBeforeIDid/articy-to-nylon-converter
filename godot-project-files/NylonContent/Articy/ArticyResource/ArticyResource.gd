class_name ArticyResource extends ArticyResourceBaseSources
## The base class for resources containing the data from an Arcity node.

var assigned_sequence: NylonSequence

var aatt_array: Array[ArticyAddToTree] = []

var creates_node: bool = true


## Executes [method _add_pin_sequence_nodes], [method _add_main_nodes], and
## [method _add_goto_nodes], then returns the modified [member aatt_array].
## [br][br]
## The converter calls this function once per [ArticyResource].
## [br][br]
## You probably don't want to override this one, and should override
## the four functions it calls instead.
func _generate_nylon() -> Array[ArticyAddToTree]:
	ArticyLog.log_ar_properties(self)
	_add_pin_sequence_nodes()
	
	_add_main_sequence_node()
	_add_main_nodes()
	
	_add_goto_nodes()
	
	return aatt_array


## The first of four overridable functions that generate [ArticyAddToTree]
## instructions and append them to [member aatt_array].
## [br][br]
## This function handles everything needed to accomodate incoming connections.
## Unless overriden, this means generating a [NylonSequence] node for each
## pin and assigning that pin's unique id as the node's name.
## [br][br]
## When you override this, you should always run [code]super[/code] before
## your custom code, unless you're extending a Container class and implementing
## custom incoming connection behaviour manually.
func _add_pin_sequence_nodes():
	for aatt in _create_pin_aatt_array():
		aatt_array.append(aatt)


## The second of four overridable functions that generate [ArticyAddToTree]
## instructions and append them to [member aatt_array].
## [br][br]
## This function creates the [NylonArticySequence] parent for the nodes that
## will be added by [method _add_main_nodes].
## [br][br]
## You should never need to override this. The [ArticyFlowFragment] class overrides
## this function to implement container behaviour, but I can't think of any
## other use-cases. If you're in a situation where you think you should override
## this function, you should probably extend [ArticyFlowFragment].
func _add_main_sequence_node():
	aatt_array.append(_create_sequence_aatt())


## The third of four overridable functions that generate [ArticyAddToTree]
## instructions and append them to [member aatt_array].
## [br][br]
## This function is responsible for creating all non-sequence and non-goto
## [NylonNode]s. (e.g The [NylonText] node for an [ArticyDialogueFragment],
## the [NylonInstruction] node for an [ArticyInstruction], or any recipe of nodes
## that you've decided to represent with a single custom template in Articy.)
## [br][br]
## When you override this function, you must always run [code]super[/code] first,
## otherwise the converter's logic will break. The sequences generated by
## [method _add_pin_sequence_nodes] are expecting to connect with the sequence
## generated by [method _create_sequence_att] within this base function, and
## they will fail to connect if it doesn't exist.
## [br][br]
## See [ArticyAddToTree] for more details on how to override this function
## with your own custom behaviour.
func _add_main_nodes():
	pass


## The last of four overridable functions that generate [ArticyAddToTree]
## instructions and append them to [member aatt_array].
## [br][br]
## This function is responsible for handling all of the [ArticyResource]'s
## outgoing connections by creating the appropriate [NylonNode]s. As implemented,
## this broadly means any [NylonNode] with the [code]target_scene[/code]
## property (specifically, it means [NylonGoToArticy], [NylonChoice], and any
## classes that inherit from those two.)
## [br][br]
## By default, the base function will generate an [ArticyAddToTree] instruction
## containing a [NylonGoToArticy] node, which will connect to the sequence 
## named [member active_pin.id]. This provides the expected behaviour
## for any non-branching node.
func _add_goto_nodes():
	aatt_array.append(create_goto_aatt())


func create_aatt() -> ArticyAddToTree:
	var aatt = ArticyAddToTree.new()
	aatt.ar = self
	return aatt


func create_goto_aatt() -> ArticyAddToTree:
	var aatt_goto: ArticyAddToTree = create_aatt()
	aatt_goto.node = NylonGoToArticy.new()
	
	if active_pin:
		aatt_goto.target_sequence_id = active_pin.id
	return aatt_goto


func _create_sequence_aatt() -> ArticyAddToTree:
	var aatt = create_aatt()
	aatt.node = NylonArticySequence.new()
	aatt.node.name = id
	return aatt


func _create_pin_aatt_array(pins = all_pins) -> Array[ArticyAddToTree]:
	var array: Array[ArticyAddToTree] = []
	
	for pin in pins:
		for aatt in pin.create_aatt_array():
			array.append(aatt)
	
	return array
